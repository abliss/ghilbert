# Creative Commons Attribution-Share Alike 3.0 Unported (http://creativecommons.org/licenses/by-sa/3.0/)
import (CLASSICAL Classical_propositional_calculus.ghi () "")
import (FIRSTORDER First-order_logic_with_quantifiability.ghi (CLASSICAL) "")
import (START Relations_1.ghi (CLASSICAL FIRSTORDER) "")
tvar (object A B C D A0 A1 B0 B1 α β Z)
var (object a b c x y z w a′ b′ x′ y′ z′)
tvar (formula φ ψ antecedent)
thm (FirstSecondCartesianProduct-A-b ((A b a) (B b a) (C b a)) ()
  (→ (∧ (= A (orderedPair b c))
    (∧ (∈ b B) (∈ c C)))
    (= (1st A) b))
        (= A (orderedPair b c))
          (∧ (∈ b B) (∈ c C))
          ConjunctionRightElimination

        A (orderedPair b c) FirstBuilder
        applySyllogism

        b c First
        introduceRightConjunctToConsequent

        (1st A) (1st (orderedPair b c)) b EqualityTransitivity
        applySyllogism
 )

thm (FirstSecondCartesianProduct-A-B ((A b a) (B b a) (C b a)) ()
  (→ (∧ (= A (orderedPair b c))
    (∧ (∈ b B) (∈ c C)))
    (∈ (1st A) B))
        (= A (orderedPair b c))
          (∧ (∈ b B) (∈ c C))
          ConjunctionLeftElimination
        eliminateRightConjunctInConsequent

        A b c B C FirstSecondCartesianProduct-A-b

        (1st A) b B MembershipBuilderRR
        applySyllogism

        eliminateBiconditionalForwardInConsequent
        applyModusPonensInConsequent
 )
thm (FirstSecondCartesianProduct-A-c ((A b a) (B b a) (C b a)) ()
  (→ (∧ (= A (orderedPair b c))
    (∧ (∈ b B) (∈ c C)))
    (= (2nd A) c))
        (= A (orderedPair b c))
          (∧ (∈ b B) (∈ c C))
          ConjunctionRightElimination

        A (orderedPair b c) SecondBuilder
        applySyllogism

        b c Second
        introduceRightConjunctToConsequent

        (2nd A) (2nd (orderedPair b c)) c EqualityTransitivity
        applySyllogism
 )

thm (FirstSecondCartesianProduct-A-C ((A b a) (B b a) (C b a)) ()
  (→ (∧ (= A (orderedPair b c))
    (∧ (∈ b B) (∈ c C)))
    (∈ (2nd A) C))
        (= A (orderedPair b c))
          (∧ (∈ b B) (∈ c C))
          ConjunctionLeftElimination
        eliminateLeftConjunctInConsequent

        A b c B C FirstSecondCartesianProduct-A-c

        (2nd A) c C MembershipBuilderRR
        applySyllogism

        eliminateBiconditionalForwardInConsequent
        applyModusPonensInConsequent
 )

thm (FirstSecondCartesianProduct ( (A c b) (B c b) (C c b)) ()
  (→ (∈ A (× B C)) (∧ (∈ (1st A) B) (∈ (2nd A) C)))
        A B C b c MembershipCartesianProduct
        eliminateBiconditionalReverse

        A b c B C FirstSecondCartesianProduct-A-B
        A b c B C FirstSecondCartesianProduct-A-C
        composeConjunction

        c addThereExists
        b addThereExists

        applySyllogism

        removeThereExistsInConsequent
        removeThereExistsInConsequent
 )
export (RESULT Set_theory.ghi (CLASSICAL FIRSTORDER) "")
